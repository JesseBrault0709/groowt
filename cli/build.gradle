plugins {
    id 'GroowtConventions'
    id 'groowt-logging'
    id 'com.jessebrault.jbarchiva' version '0.1.0'
    id 'maven-publish'
}

group = 'groowt'
version = '0.1.0'

repositories {
    maven {
        url 'https://repo.gradle.org/gradle/libs-releases'
    }
}

dependencies {
    [
            libs.gradle.tooling,
            libs.picocli,
            libs.slf4j.api,
            libs.log4j.core,
            project(':groowt-gradle-model')
    ].each { implementation it }

    runtimeOnly libs.log4j.slf4jBinding
}

tasks.named('jar', Jar) {
    manifest {
        attributes('Main-Class': 'groowt.cli.GroowtCli')
    }
    from sourceSets.main.runtimeClasspath.filter(File.&exists).collect { it.isDirectory() ? it : zipTree(it) }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn ':groowt-gradle-model:jar'
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        create('groowtCli', MavenPublication) {
            artifactId = 'groowt-cli'
            from components.java
            pom {
                withXml {
                    def rootNode = asNode()
                    def dependenciesNode = rootNode.get('dependencies')
                    rootNode.remove(dependenciesNode)
                }
            }
        }
    }
}
