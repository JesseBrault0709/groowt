plugins {
    id 'groowt-conventions'
    id 'groowt-logging'
    id 'groowt-publish'
}

repositories {
    maven {
        url 'https://repo.gradle.org/gradle/libs-releases'
    }
}

dependencies {
    implementation libs.gradle.tooling
    implementation libs.picocli
    implementation project(':groowt-gradle-model')
}

tasks.named('jar', Jar) {
    manifest {
        attributes('Main-Class': 'groowt.cli.GroowtCli')
    }
    from sourceSets.main.runtimeClasspath.filter(File.&exists).collect { it.isDirectory() ? it : zipTree(it) }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn ':groowt-gradle-model:jar'
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

publishing {
    publications {
        create('groowtCli', MavenPublication) {
            artifactId = 'groowt-cli'
            from components.java
            pom {
                withXml {
                    def rootNode = asNode()
                    def dependenciesNode = rootNode.get('dependencies')
                    rootNode.remove(dependenciesNode)
                }
            }
        }
    }
}
